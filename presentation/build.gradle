apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

Properties props = new Properties()
props.load(new FileInputStream(file("../config/signing.properties")))
props.load(new FileInputStream(file("../config/key.properties")))

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        applicationId "com.jude.emotionshow"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        renderscriptTargetApi 23
        renderscriptSupportModeEnabled true
        manifestPlaceholders = [
                UMENG_APPKEY    : props['UMENG_APPKEY'],
                AMAP_APPKEY     : props['AMAP_APPKEY']
        ]

    }

    productFlavors {
        xiaomi{}
        baidu{}
        wandoujia{}
    }

    productFlavors.all { flavor ->
        manifestPlaceholders.put("UMENG_CHANNEL",name)
    }

    signingConfigs {
        release{
            keyAlias props['KEY_ALIAS']
            keyPassword props['KEY_PASSWORD']
            storeFile file(props['KEYSTORE_FILE'])
            storePassword props['KEYSTORE_PASSWORD']
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    def presentationDependencies = rootProject.ext.presentationDependencies
    compile presentationDependencies.card
    compile presentationDependencies.appcompat
    compile presentationDependencies.design
    compile presentationDependencies.card
    compile presentationDependencies.beam
    compile presentationDependencies.swipeBackHelper
    compile presentationDependencies.butterKnife
    compile (presentationDependencies.utils)
    compile (presentationDependencies.materialDialogsCore){ transitive = true }
    compile presentationDependencies.tagview
    compile presentationDependencies.rollviewpager
    compile presentationDependencies.picasso
    compile presentationDependencies.eventBus
    compile presentationDependencies.rxAndroid
    compile presentationDependencies.rxJava
    compile presentationDependencies.exgridview
    compile presentationDependencies.umengAnalytics
    compile presentationDependencies.imageprovider
    compile presentationDependencies.ripple
    compile presentationDependencies.recyclerViewHeader
    compile presentationDependencies.pagerSlidingTabStrip
    compile project(':data')
    compile project(':domain')
    compile project(':SMSSDK')
}
